/**
 * Module:      inst_user.ycp
 *
 * Authors:     Klaus Kaempf <kkaempf@suse.de>
 *              Stefan Hundhammer <sh@suse.de>
 *
 * Purpose:     Start user management module from within installation workflow
 *
 * $Id$
 */
{

    textdomain "users";

    import "Wizard";
    import "Mode";
    import "Product";
    import "Users";

    include "require.ycp";

    // no user for imap server cd, y2c_imap will create it
    if (Product::imap_server || Product::openteam_server || Product::fwadmin_host)
        return `auto;

    define boolean check_for_package (string package) ``{

        return RequireAndConflictButtons ( [package], [],
            // popup label (%1 is package to install)
            sformat (_("Package %1 is not installed.
Install it now?
"), package), UI::OKButtonLabel(), UI::CancelButtonLabel());

    }

    // what to call after inst_auth dialog
    if (Users::after_auth == `nis)
    {
        if (check_for_package ("yast2-nis-client"))
        {
            return CallFunction ("nis", WFM::Args ());
        }
        else
            return `back;
    }
    else if (Users::after_auth == `nisplus)
    {
        if (check_for_package ("yast2-nisplus-client"))
        {
            return CallFunction ("nisplus", WFM::Args ());
        }
        else
            return `back;
    }
    else if (Users::after_auth == `ldap)
    {
        if (check_for_package ("yast2-ldap-client"))
        {
            return CallFunction ("ldap", WFM::Args ());
        }
        else
            return `back;
    }

    // else run the users module
    string start_mod = "user_inst_start";
    term   users_mod = `users( start_mod );

    if ( Mode::test )
        users_mod = add( users_mod, .test );

    y2debug( "Starting user management module: %1", users_mod );
    any ret = CallFunction( users_mod );
    y2debug( "User management module returned %1", ret );

    if ( ret == nil )
    {
        y2warning( "Couldn't call 'users' module - is it installed?" );
        ret = `auto;
    }

    return ret;
}
