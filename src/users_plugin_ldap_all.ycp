/**
 * File:
 *	users_plugin_ldap_all.ycp
 *
 * Package:
 *	Configuration of Users
 *
 * Summary:
 *	This is part GUI of UsersPluginLDAPAll - plugin for editing all LDAP
 *	user/group attributes.
 *
 * $Id$
 */


{
    textdomain "users"; // use own textdomain for new plugins

    import "Label";
    import "Popup";
    import "Report";
    import "Wizard";

    import "Ldap";
    import "LdapPopup";
    import "Users";
    import "UsersLDAP";
    import "UsersPluginLDAPAll"; // plugin module

    any ret = nil;
    string func			= "";
    map<string,any> config	= $[];
    map<string,any> data	= $[];

    /* Check arguments */
    if(size(WFM::Args()) > 0 && is(WFM::Args(0), string)) {
	func = (string) WFM::Args(0);
	if(size(WFM::Args()) > 1 && is(WFM::Args(1), map))
	    config = (map<string,any>)WFM::Args(1);
	if(size(WFM::Args()) > 2 && is(WFM::Args(2), map))
	    data = (map<string,any>)WFM::Args(2);
    }
    y2milestone("----------------------------------------");
    y2milestone("users plugin started: LDAPAll");

    y2debug ("func=%1", func);
    y2debug ("config=%1", config);
    y2debug ("data=%1", data);

    if (func == "Summary") {
	ret = UsersPluginLDAPAll::Summary (config, $[]);
    }
    else if (func == "Name") {
	ret = UsersPluginLDAPAll::Name (config, $[]);
    }
    else if (func == "Dialog") {
	// define the dialog for this plugin and return it's contents

	string caption	= UsersPluginLDAPAll::Name (config, $[]);
	string what	= config["what"]:"user";


	// helptext 1/3
	string help_text = _("<p>
Here, see the table of all allowed attributes for the current LDAP entry that were not set in previous dialogs.</p>") +

	// helptext 1/3 (don't translate objectClass"),
        // %1 is list of values
	sformat (_("<p>
The list of attributes is given by the value of \"objectClass\"
(which is currently:
<br>%1).
</p>
"), mergestring (what == "user" ?
	UsersLDAP::GetUserClass () : UsersLDAP::GetGroupClass (), ",<br>")) +

	// helptext 3/3
	_("<p>
Edit each attribute using <b>Edit</b>. Some attributes 
could be required, as defined in the user template in the <b>LDAP Client Module</b>.</p>
");

	map<string, any> tmp_data	= $[];
        list items			= [];
        list used_attributes	= [];
        list new_attributes		= [];
        boolean modified		= false;

	// list of attributes that are edited elsewhere
        // They don't have to really be set for current user! (e.g.shadow*)
        // (There are LDAP names for shadow* in this list -> they cannot be in
        // ldap2yast, because no "translation to yast names" is needed.)
        list already_edited_keys = (what == "user") ?
	[ "username", "uidNumber", "homeDirectory", "givenName", "sn",
	"userPassword", "objectClass", "loginShell", "gidNumber",
	"shadowLastChange", "shadowWarning", "shadowInactive", "shadowExpire",
	"shadowMin", "shadowMax", "shadowFlag" ] :
	// and now for groups
	[ "groupname", "gidNumber", "userPassword", "objectClass", "userlist",
	  UsersLDAP::GetMemberAttribute () ];
	// FIXME problem when these parameters are required!

	// keys in user's map which are not saved anywhere
	list internal_keys = (what == "user") ?
	    UsersLDAP::GetUserInternal () : UsersLDAP::GetGroupInternal ();

	list required_attrs = (what == "user") ?
	    UsersLDAP::GetUserRequiredAttributes () :
	    UsersLDAP::GetGroupRequiredAttributes ();

	// show only attributes allowed by schema

	list allowed_attrs= Ldap::GetObjectAttributes (data["objectClass"]:[]);

	// generate table items from already existing values
        foreach (string attr, any val, data, ``{
	    if (contains (internal_keys, attr))
		return;
	    if (contains (already_edited_keys, attr))
		return;
	    if (!contains (allowed_attrs, attr))
		return;
	    if (is (val, map) || val == nil)
		return;
	    list<string> value = [];
	    if (is (val, string))
	    {
		value = [ (string)val ];
		data [attr] = value;
	    }
	    else value = (list<string>)val;
	    used_attributes = add (used_attributes, attr);
	    items = add (items,`item (`id(attr), attr, mergestring(value,",")));
	});

	// generate table items with empty values
	// (not set for this user/group yet)
	// we need to read available attributes from Ldap
	map ldap2yast = (what == "user") ? UsersLDAP::GetUserAttrsLDAP2YaST():
	    UsersLDAP::GetGroupAttrsLDAP2YaST();

	foreach (string class, data["objectClass"]:[], ``{
	    foreach (string at, Ldap::GetAllAttributes (class), ``{
		// remove already used (uid, uidNumber, homeDirectory etc.)
		if (!haskey (data, ldap2yast [at]:at) &&
		    !contains (already_edited_keys, ldap2yast [at]:at))
		{
		    data[at] = [];
		    new_attributes = add (new_attributes, at);
		    items = add (items, `item (`id(at), at, ""));
		}
	    });
	});

	term contents = `HBox(`HSpacing (1.5), `VBox(
	    `VSpacing(0.5),
	    `Table(`id(`table), `opt(`notify), `header(
		// table header 1/2
		_("Attribute"),
		// table header 2/2
		_("Value")),
		items),
	    `HBox (
		`PushButton(`id(`edit), `opt(`key_F4), Label::EditButton()),
		`HStretch()
	    ),
	    `VSpacing (0.5)
	    ),
	    `HSpacing (1.5)
	);

	Wizard::CreateDialog ();

	// dialog caption
	Wizard::SetContentsButtons(_("Additional LDAP Settings"),
	    contents, help_text, Label::BackButton(), Label::NextButton());

	ret = `next;
        UI::SetFocus (`id(`table));
	repeat
	{
	    ret = UI::UserInput();
	    if ( ret == `edit || ret == `table)
	    {
		string attr		=
		    (string) UI::QueryWidget (`id(`table), `CurrentItem);
		list<string> value	= tmp_data [attr]:data[attr]:[];
		value			= LdapPopup::EditAttribute (
		    attr, value, [], Ldap::SingleValued (attr), []);
		if (value == tmp_data [attr]:data[attr]:[])
		{
		    ret = `notnext;
		    continue;
		}
		UI::ChangeWidget (`id(`table),`Item(attr,1),
		    mergestring(value,","));
		tmp_data [attr] = value;
	    }
	    if ( ret == `next )
	    {
		string err = UsersPluginLDAPAll::Check (
		    config,
		    (map<string,any>) union (data, tmp_data));
		// FIXME what about old (internal) parameters (givenName) ???
		// they are not shown in this dialog but they could be missing!
		if (err != "")
		{
		    Report::Error (err);
		    ret = `notnext;
		    continue;
		}

		if (tmp_data == $[])
		{
		    break;
		}
		// FIXME: use Plugin::Edit/Add or Users::Update?
		if (data["what"]:"" == "edit_user")
		{
		    Users::EditUser (tmp_data);
		}
		else if (data["what"]:"" == "add_user")
		{
		    Users::AddUser (tmp_data);
		}
		else if (data["what"]:"" == "edit_group")
		{
		    Users::EditGroup (tmp_data);
		}
		else if (data["what"]:"" == "add_group")
		{
		    Users::AddGroup (tmp_data);
		}
	    }
	} until (contains ([`next, `abort, `back, `cancel], ret));
	Wizard::CloseDialog ();
    }
    /* unknown function */
    else {
	y2error("unknown function: %1", func);
	ret = false;
    }

    y2debug ("ret=%1", ret);
    y2milestone("users plugin finished");
    y2milestone("----------------------------------------");

    return ret;
}
