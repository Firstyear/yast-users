/**
 * small client to test the minimum features (e.g. command-line) of new
 * Users module
 */

{

textdomain "users";

y2milestone ("----------------------------------------");
y2milestone ("Users module started");

import "Users";
import "Mode";
import "Ldap"; //FIXME workaround to initialize variables
import "CommandLine";

include "users/wizards.ycp";

string  start_dialog	= "summary";

any	ret		= nil;
integer arg		= 0;
list    possible_start_dialogs = ["user_add", "group_add", "summary", "user_inst_start", "users", "groups" ];

while ( arg < size( WFM::Args() ) )
{
    any a = WFM::Args( arg );
    if ( is (a, string) && contains( possible_start_dialogs, a))
    {
	start_dialog = (string) WFM::Args( arg );
    }
    arg = arg + 1;
}

if (start_dialog == "users" || start_dialog == "groups")
{
    UsersCache::SetCurrentSummary (start_dialog);
    start_dialog = "summary";
}

if (start_dialog != "summary")
{
    y2milestone( "Starting with %1 - test mode: %2", start_dialog, Mode::test );
    ret = UsersSequence (start_dialog);
    y2milestone("Users module finished with %1", ret);
    y2milestone("----------------------------------------");
    return ret;
}

// else parse arguments in cmdline
y2milestone( "Starting with arguments: %1", WFM::Args() );


// --------------------------------------------------------------------------
// --------------------------------- cmd-line handlers

/**
 * List users
 * @return boolean false
 */
define boolean UsersListHandler (map options ) ``{

    list sets = maplist (string type, any val, options, ``(type));
    if (sets == [])
	sets = ["local"];

    foreach (string type, sets, ``{
	if (type == "nis" && Users::NISAvailable () && Users::NISNotRead ())
	{
	    Users::ReadNewSet ("nis");
	}
	foreach (string username, UsersCache::GetUsernames (type), ``{
	    CommandLine::Print (username);
	});
    });

    return false; // do not call Write...
}

/**
 * Show one user information
 * @return boolean false
 */
define boolean UsersShowHandler (map options ) ``{

    map user = $[];
    integer uid = tointeger (options ["uidNumber"]:options["uid"]:"-1");
    string username = options ["username"]:"";

    if (uid != -1)
    {
	user = Users::GetUser (uid, "");
    }
    else if (username != "")
    {
	user = Users::GetUserByName (username, "");
    }

    string out = "";
    map keys = $[
	// label shown at command line (user attribute)
	"cn"		: _("Full Name:"),
	// label shown at command line (user attribute)
	"username"	: _("Login Name:"),
	// label shown at command line (user attribute)
	"homeDirectory"	: _("Home Directory:"),
	// label shown at command line (user attribute)
	"loginShell"	: _("Login Shell:"),
	// label shown at command line (user attribute)
	"uidNumber"	: _("UID:"),
	// label shown at command line (user attribute)
	"groupname"	: _("Default Group:"),
	// label shown at command line (user attribute)
	"grouplist"	: _("List of Groups:"),
    ];
    foreach (string key, any value, user, ``{
	key = keys[key]:"";
	if (key == "") return;
	string svalue = sformat ("%1", value);
	if (is (value, map))
	{
	    svalue = mergestring ((list<string>)
		maplist (string k, any v, (map) value, ``(k)), ",");
	}
	CommandLine::Print (sformat ("%1\n\t%2", key, svalue));
    });

    return false; // do not call Write...
}

/**
 * Add user
 * @return boolean false
 */
define boolean UsersAddHandler (map <string, any> options ) ``{

    if (options["username"]:"" == "")
    {
	y2error ("You must specify username");
	return false;
    }

    map <string, any> user = $[];
    map keys = $[
	"password"	: "userPassword",
	"home"		: "homeDirectory",
	"shell"		: "loginShell",
	"fullname"	: "cn",
	"gid"		: "gidNumber",
	"uid"		: "uidNumber",
	"no_home"	: "create_home"
    ];
    foreach (string key, any value, options, ``{
	if (key == "no_home")
	    value = false;
	user [ keys[key]:key ] = value;
    });

    Users::AddUser ($[]);
    Users::AddUser (user);

    string error = Users::CheckUser ($[]);
    if (error != "")
    {
	CommandLine::Print (error);
	return false;
    }

    return Users::CommitUser ();
}

/**
 * Delete user
 * @return boolean false
 */
define boolean UsersDeleteHandler (map  <string, any>options ) ``{

    if (options["username"]:"" == "")
    {
	y2error ("You must specify username");
	return false;
    }
    boolean delete_home = false;

    Users::SelectUserByName (options["username"]:"");
    Users::DeleteUser (delete_home);
    Users::CommitUser ();
    return true;
}

define boolean UsersRead () ``{

    return Users::Read ();
}

define boolean UsersWrite () ``{

    return Users::Write () == "";
}

define boolean UsersGUI ()``{
    return (UsersSequence (start_dialog) == `next);
}


/* the command line description map */
map cmdline = $[
    "id"		: "users",
    // translators: command line help text for Users module
    "help"		: _("Users configuration module."),
    "guihandler"	: UsersGUI,
    "initialize"	: UsersRead,
    "finish"		: UsersWrite,
    "actions"		: $[
	"list" :$[
	    "handler"	: UsersListHandler,
	    // translators: command line help text for list action
	    "help"	: _("List of available users")
	],
	"show" :$[
	    "handler"	: UsersShowHandler,
	    // translators: command line help text for show action
	    "help"	: _("Show information of selected user")
	],
	"add" :$[
	    "handler"	: UsersAddHandler,
	    // translators: command line help text for ad action
	    "help"	: _("Add new user")
	],
	"delete" :$[
	    "handler"	: UsersDeleteHandler,
	    // translators: command line help text for delete action
	    "help"	: _("Delete an existing user. (Home directory is not removed.)" )
	],
    ],
    "options"		: $[
	"local"	:$[
	    // translators: command line help text for list local option
	    "help"	: _("List of local users"),
	],
	"system"	:$[
	    // translators: command line help text for list system option
	    "help"	: _("List of system users"),
	],
	"ldap"		:$[
	    // translators: command line help text for list ldap option
	    "help"	: _("List of LDAP users"),
	],
	"nis"		:$[
	    // translators: command line help text for list nis option
	    "help"	: _("List of NIS users"),
	],
	"uid"		:$[
	    // translators: command line help text for show uid option
	    "help"	: _("UID of the user"),
	    "type"	: "string"
	],
	"username"	:$[
	    // translators: command line help text for username option
	    "help"	: _("Login Name of the user"),
	    "type"	: "string"
	],
	"cn"		:$[ //TODO enable aliases?
	    // translators: command line help text for add option
	    "help"	: _("Full Name of the user"),
	    "type"	: "string"
	],
	"home"	:$[
	    // translators: command line help text for username option
	    "help"	: _("Home directory of the user"),
	    "type"	: "string"
	],
	"no_home"	:$[
	    // translators: command line help text for show create_home option
	    "help"	: _("Do not create home directory for new user."),
	],
	"password"		:$[
	    // translators: command line help text for add option
	    "help"	: _("Password of the user"),
	    "type"	: "string"
	],
    ],
    "mappings"		: $[
	"list"	: [ "local", "system", "ldap", "nis" ],// + "custom"
	"show"  : [ "uid", "username"],
	"add"	: [ "username", "uid", "cn", "password", "home", "no_home" ],
	"delete": [ "username" ],
//	"read"	: [ "nis", "ldap" ] TODO
// TODO set the default values ("useradd")
    ]
];

Users::SetGUI (false);
ret = CommandLine::Run (cmdline);

/* Finish */
y2milestone("Users module finished with %1", ret);
y2milestone("----------------------------------------");
return ret;

} //EOF
