/**
 * File:
 *   users_auto.ycp
 *
 * Package:
 *   Configuration of Users
 *
 * Summary:
 *   Client for autoinstallation
 *
 * Authors:
 *   Anas Nashif <nashif@suse.de>
 *
 * $Id$
 *
 * This is a client for autoinstallation. It takes its arguments,
 * goes through the configuration and return the setting.
 * Does not do any changes to the configuration.
 */


{
    textdomain "users";
    import "Mode";
    import "Users";
    import "Wizard";

    include "users/wizards.ycp";
    include "wizard/sequencer.ycp";

    any ret = nil;
    string func = "";
    map param = $[];
    /* Check arguments */
    if(size(WFM::Args()) > 0 && is(WFM::Args(0), string)) {
	func = (string) WFM::Args(0);
	if(size(WFM::Args()) > 1 && is(WFM::Args(1), map))
	    param = (map)WFM::Args(1);
    }
    y2debug("func=%1", func);
    y2debug("param=%1", param);


    list users = [
        $[ "username":"my_user",
           "user_password":"pass",
           "encrypted": false,
           "uid":501,
           "gid":100,
           "password_settings":$[
                "expire":""
            ]
        ],
        $[ "username":"daemon",
           "user_password":"pass",
           "encrypted": false,
        ],
        $[ "username":"root",
           "user_password":"pass",
        ]
     ];

//    param = $["users": users];

    block<boolean> abort_block = ``{ return false; };

    if (func == "Import") {
	ret = Users::Import(param);
    }
    /* create a  summary*/
    else if (func == "Summary") {
	ret =  Users::Summary();
    }
    else if (func == "Reset") {
	Users::Import($[]);
	ret = $[];
    }
    else if (func == "Packages") {
		ret = $[];
	}
    else if (func == "Change") {
	string start_dialog = "users"; //look to users.ycp for possible values
	Wizard::CreateDialog();
	ret = AutoSequence(start_dialog);
	Wizard::CloseDialog();
    }
    else if (func == "Export") {
	ret = Users::Export();
	Users::ExportAll = false;
    }
    else if (func == "Read") {
	import "Progress";
	Users::ExportAll = true;
	Progress::off();
	ret = Users::Read (abort_block, false);
	Progress::on();
    }
    else if (func == "Write") {
	import "Progress";
	Users::write_only = true;
	Progress::off();
	ret = Users::Write (abort_block, false);
	Progress::on();
    }
    /* unknown function */
    else {
	y2error("unknown function: %1", func);
	ret = false;
    }

    y2debug("ret=%1", ret);
    y2milestone("users auto finished");
    y2milestone("----------------------------------------");

    return ret;

}
