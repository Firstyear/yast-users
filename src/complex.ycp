/**
 * File:	include/users/complex.ycp
 * Package: Configuration of users and groups
 * Summary:	Dialogs definitions
 * Authors:	Johannes Buchhold <jbuch@suse.de>,
 *          Jiri Suchomel <jsuchome@suse.cz>
 *
 * $Id$
 */

{

textdomain "users";

import "Wizard";
import "Mode";
import "Report";
import "Security";

import "Users";
import "UsersCache";

import "Popup";
import "Label";

include "users/helps.ycp";

/**
 * Return a modification status
 * @return true if data was modified
 */
global define boolean Modified() ``{
    return (Users::users_modified || Users::groups_modified ||
        Users::customs_modified || Users::ldap_modified ||
        Users::defaults_modified || Users::security_modified);
}


/**
 * Read settings dialog
 * @param useUI boolean use user interface (change progress bar)
 * @return symbol `next if success, else `abort
 */
global define symbol ReadDialog (boolean useUI ) ``{

    // Set help text
    if ( useUI ) Wizard::RestoreHelp ( ReadDialogHelp () );

    // A callback function for abort
    block abort = ``{
        return UI::PollInput () == `abort;
    };

    symbol ret = Users::Read(abort, useUI);
    return ret;
}

/**
 * Write settings dialog
 * @param useUI boolean use user interface (change progress bar)
 * @return symbol `next if success, else `abort
 */
global define symbol WriteDialog (boolean useUI ) ``{

    // Set help text
    if ( useUI ) Wizard::RestoreHelp ( WriteDialogHelp () );

    // A callback function for abort
    block abort = ``{
        // popup text
        if (UI::PollInput () == `abort && Popup::YesNo (_("Really abort the writing process?")))
            return true;
        return false;
    };
    return Users::Write(abort, useUI);
}

/**
 * Set the module to normal setup mode
 * @return symbol for wizard sequencer
 */
global define symbol userSetup () ``{
    Users::start_dialog = "users" ;
    return `next;
}

/**
 * Set the module into installation mode with
 * first dialog for single user addition
 * @return symbol for wizard sequencer
 */
global define symbol usersInstStart () ``{
    Users::start_dialog = "user_add";// ??
    return `next;
}


/**
 * The dialog that appears when the [Abort] button is pressed.
 * @return `abort if user really wants to abort
 */
global define symbol ReallyAbort() ``{

    boolean ret = true;

    if ( ! Mode::cont )
    {
        if (Modified())
            ret = Popup::ReallyAbort (true);
    }
    else
    {
        ret = CallFunction(`inst_confirm_abort(`incomplete));
    }

    if( ret )
    {
        return `abort;
    }
    else return `back;
}


/**
 * The first of the two main (summary) dialogs -- list of the users.
 * @return symbol for wizard sequencer
 */
global define symbol UsersDialog() ``{

    /**
     * Helper function for reading again current user/group lists if necessary
     * For use inside UsersDialog
     */
    global define void re_read_ldap () ``{

	// re-read the list when LDAP are shown
	if (contains (UsersCache::current_users, "ldap"))
	{
	    string set = (size(UsersCache::current_users) == 1)?"ldap":"custom";
	    Users::ChangeCurrentUsers (set);
	    UsersCache::user_itemlist = UsersCache::users_itemlists[set]:[];
	    UI::ChangeWidget(`id(`user_table), `Items,
		UsersCache::user_itemlist);
	}
	// modify the list of groups also!
	if (contains (UsersCache::current_groups, "ldap"))
	{
	    string set = (size(UsersCache::current_groups)== 1)?"ldap":"custom";
	    Users::ChangeCurrentGroups (set);
	    UsersCache::group_itemlist = UsersCache::groups_itemlists[set]:[];
	}
    }

    list expert_list = [
	    // menubutton label
	    `item(`id(`defaults), _("De&faults for New Users")),
    ];
    if (!Mode::config)
    {
	expert_list = merge (expert_list, [
	    // menubutton label
	    `item(`id(`enc), _("Password &Encryption")),
	    // menubutton label
	    `item(`id(`auth), _("&Authentication and User Sources"))
	]);
	if (Users::is_ldap_available)
	{
	    expert_list = add (expert_list,
		// menubutton label
		`item(`id(`ldapconf), _("Configure L&DAP Client")));
	    if (!Users::ldap_modified)
	    {
		expert_list = add (expert_list,
		    // menubutton label
		    `item(`id(`ldapfilter), _("LDAP &Search Filter")));
	    }
	}
    }

    /*
    symbol current_filter = "local";//current users!
    list available_filters = [];
    foreach (`type, Users::available_usersets, ``{
	if (current_filter == type)
	    available_filters = add (available_filters, `item(`id(type),
		deletechars (Users::userset_to_string[type]:"", "&"), true));
	else
	    available_filters = add (available_filters,	`item(`id(type),
		deletechars (Users::userset_to_string[type]:"", "&"), false));
    });

    available_filters = add (available_filters,
        // PushButon
	`item (`id(`customize), _("Customize Filter..."), false));
    */

    Users::start_dialog = "users";
    term contents = `VBox(
        `ReplacePoint (`id(`rp), rbUsersGroup(`users)),
        `Table(`id(`user_table), `opt(`notify),
            // table header
            `header( _("Login"),
            // table header
             _("Name"),
            // table header
             _("UID"),
            // table header
             _("Groups")), [] ), // do not read until the dialog is displayed
        `HBox (
            `PushButton(`id(`new),`opt(`key_F3), Label::AddButton ()),
            `PushButton(`id(`edit), `opt(`key_F4), Label::EditButton ()),
            `PushButton(`id(`delete), `opt(`key_F5), Label::DeleteButton ()),
            `HStretch()),
	/*
	`VSquash(
	`HBox(
	    `ComboBox(`id(`usersets), `opt(`notify, `key_F2), _("Filter"),
		    available_filters),
	    `VBox (`VStretch(),
                // PushButon
	        `MenuButton(`id(`expertlist), _("E&xpert options..."),
	            expert_list)
	    ),
            `HStretch()
	))
	*/
        `HBox(
            // MenuButton label
            `MenuButton(`id(`usersets), `opt(`key_F2), _("&Set Filter"),
                Users::available_usersets_items),
            // PushButon
            `PushButton(`id(`customize), `opt(`key_F6), _("Customi&ze Filter")),
            // PushButon
            `MenuButton(`id(`expertlist), _("E&xpert options..."),
                expert_list),
            `HStretch()
	)
    );

    string lastbutton =  Label::FinishButton() ;
    if( Mode::cont )
        lastbutton =  Label::NextButton() ;

    Wizard::SetContentsButtons(
                    // dialog caption
                    _("User and Group administration"),
                    contents,
                    UsersDialogHelp(),
                    Label::BackButton(),
                    lastbutton );

    UI::ChangeWidget (`id (`user_table), `Items, UsersCache::user_itemlist);

    if (size (UsersCache::user_itemlist) > 0)
	UI::SetFocus (`id(`user_table));

    if( UsersCache::focusline_user != nil)
        UI::ChangeWidget(`id(`user_table), `CurrentItem, UsersCache::focusline_user );

    any ret = `next;
    repeat
    {
        ret = UI::UserInput();

	// FIXME combobox:
	/*
	if (ret == `usersets)
	{
	    ret = `not_next;
	    if (current_filter != UI::QueryWidget (`id(`usersets), `Value))
	    {
		y2internal ("new value: %1", UI::QueryWidget (`id(`usersets), `Value));
		current_filter = UI::QueryWidget (`id(`usersets), `Value);
		ret = current_filter;
	    }
	}
	*/
        if ( contains (Users::available_usersets, ret) )
        {
            if (Users::ChangeCurrentUsers (ret))
            {
                UsersCache::user_itemlist = UsersCache::users_itemlists[ret]:[];

                UI::ChangeWidget(`id(`user_table),`Items,
                    UsersCache::user_itemlist);
                if (ret == "custom")
                    UsersCache::customized_usersview = true;
                else
                    UsersCache::customized_usersview = false;
                UI::ReplaceWidget (`id(`rp), rbUsersGroup (`users));
            }
            ret = `not_next;
        }
        if ( ret == `switch_users)
        {
            ret = `not_next;
        }
        if ( ret == `customize)
        {
            if ( CustomizePopup ("user") && UsersCache::customized_usersview)
                UI::ChangeWidget(`id(`user_table), `Items,
                    UsersCache::user_itemlist);
            UI::ReplaceWidget (`id(`rp), rbUsersGroup (`users));
            ret = `not_next;
        }

        if ( ret == `ldapconf )
        {
	    // run LDAP client now
            if (`next == CallModule("ldap", []))
	    {
		// yes/no popup
		if (!Users::ldap_modified && Popup::YesNo (_("Re-read all data from LDAP server?")))
		{
		    Users::ldap_not_read = true;
		    Users::ldap_filters_read = false;
		    Users::ldap_initialized = false;
		    Users::ReadLDAPSettings();
		    re_read_ldap();
		}
	    }
            ret = `not_next;
        }
	if ( ret == `ldapfilter )
	{
	    // change of search filter (only when LDAP was not modified yet)
	    if (LDAPSearchFilterPopup())
	    {
		Users::ldap_not_read = true;
		re_read_ldap ();
	    }
            ret = `not_next;
	}
        if ( ret == `enc)
        {
            ret = `not_next;
	    string enc = EncryptionDialog();
	    if (enc != Users::encryptionMethod)
	    {
		Users::encryptionMethod = enc;
		Users::max_length_pas = Security::PasswordMaxLengths [
		    Users::encryptionMethod]:8;
		Users::security_modified = true;
	    }
        }
        if ( ret == `user_table )
            ret = `edit;
        if ( ret == `edit  || ret == `delete )
        {
            any selected = UI::QueryWidget(`id(`user_table), `CurrentItem);
            if (selected != nil)
            {
                Users::SelectUser( selected );
                if (ret == `delete)
                    ret = DeleteUserPopup();
            }
            else
            {
                // error popup
                Report::Message(_("Please select an entry in the user table"));
                ret = `not_next;
            }
        }
        if ( ret == `new )
        {
            if (Users::kerberos_auth == "required")// only this ??
            {
                // error popup
                Report::Message (_("As you are using only Kerberos for authentication, you cannot add local users."));
                ret = `not_next;
            }
            Users::user_in_work = $[];
        }

        if ( ret == `next && Mode::cont && !Users::users_modified)
        {
            // yes-no popup contents
            if ( !Popup::YesNo( _("You have not added any user. This only makes sense
in a network environment with an authentication server.

Are you sure?")))
                ret = `not_next;
        }

    } until ( ret != `not_next );

    if (ret == `back)
    {
        if (Mode::cont)
        {
            Users::start_dialog = "user_add";
            Users::StartAgain ();
        }
        Users::use_next_time = true;
    }
    save_focus( UI::QueryWidget(`id(`user_table), `CurrentItem), ret, `user);

    if (!Mode::cont && ret == `next && !Modified())
        ret = `nosave;

    return ret;
}



/**
 * The second of the two main dialogs.
 * @return symbol for wizard sequencer
 */
global define symbol GroupsDialog() ``{

    term contents = `VBox(
        `ReplacePoint (`id(`rp), rbUsersGroup(`groups)),
        `Table(`id(`group_table ), `opt(`notify),
            // table header
             `header(_("Group name"),
            // table header
            _("Group ID"),
            // table header
            _("Group members")),
            []),// defer the reading until the dialog is displayed
        `HBox (
            `PushButton(`id(`new),`opt(`key_F3), Label::AddButton ()),
            `PushButton(`id(`edit), `opt(`key_F4), Label::EditButton ()),
            `PushButton(`id(`delete), `opt(`key_F5), Label::DeleteButton ()),
            `HStretch()),
        `HBox(
            // MenuButton label
            `MenuButton(`opt(`key_F2), _("&Set Filter"),
                Users::available_groupsets_items),
            // PushButyon
            `PushButton(`id(`customize), `opt(`key_F6), _("Customi&ze Filter")),
            `HStretch())
    );

    string lastbutton = Label::FinishButton();
    if( Mode::cont )
        lastbutton =  Label::NextButton();

    // dialog caption
    Wizard::SetContentsButtons(_("User and Group administration"),
                        contents,
                        GroupsDialogHelp(),
                        Label::BackButton(),
                        lastbutton );

    UI::ChangeWidget (`id (`group_table), `Items, UsersCache::group_itemlist);

    if (size (UsersCache::group_itemlist) > 0)
	UI::SetFocus (`id(`group_table));

    if( UsersCache::focusline_group != nil )
        UI::ChangeWidget(`id(`group_table), `CurrentItem, UsersCache::focusline_group );

    // save marked table entry to group_in_work
    any ret = `next;
    repeat
    {
        ret = UI::UserInput();

        if( contains (Users::available_groupsets, ret) )
        {
            if (Users::ChangeCurrentGroups (ret))
            {
                UsersCache::group_itemlist=UsersCache::groups_itemlists[ret]:[];

                UI::ChangeWidget(`id(`group_table), `Items,
                    UsersCache::group_itemlist);
                if (ret == "custom")
                    UsersCache::customized_groupsview = true; // already set
                else
                    UsersCache::customized_groupsview = false;
                UI::ReplaceWidget (`id(`rp), rbUsersGroup (`groups));
                ret = `not_next;
            }
        }
        if( ret == `customize)
        {
            if ( CustomizePopup ("group") && UsersCache::customized_groupsview)
                UI::ChangeWidget(`id(`group_table), `Items,
                    UsersCache::group_itemlist);
            UI::ReplaceWidget (`id(`rp), rbUsersGroup (`groups));
            ret = `not_next;
        }

        // double click
        if( ret == `group_table )
            ret = `edit;

        if( ret == `switch_groups)
        {
            ret = `not_next;
        }

        if ( ret == `edit  || ret == `delete )
        {
            integer selected = UI::QueryWidget(`id(`group_table), `CurrentItem);
            if( selected != nil )
            {
                Users::SelectGroup( selected );
                UsersCache::group_type = Users::group_in_work["type"]:"local";
                if (UsersCache::group_type == "nis")
                {
                    // error popup
                    Report::Message (_("To edit or delete a NIS group,
you must do it on server.
"));
                    ret = `not_next;
                }
                else if (ret == `delete)
                    ret = DeleteGroupPopup();
            }
            else
            {
                // error popup
                Report::Error(_("Please select an entry in the group table"));
                ret = `not_next;
            }
        }
        if ( ret == `new )
        {
            Users::group_in_work = $[];
        }
    } until ( ret != `not_next  );

    save_focus( UI::QueryWidget(`id(`group_table), `CurrentItem), ret, `group);

    if (!Mode::cont && ret == `next && !Modified())
        ret = `nosave;

    return ret;
}

/**
 * Popup for deleting user
 * @return symbol for sequencer
 */
global define symbol DeleteUserPopup() ``{

    boolean delete        = true;
    boolean delete_home   = false;
    string username       = Users::user_in_work["username"]:"";
    string home =Users::user_in_work["org_home"]:Users::user_in_work["home"]:"";
    integer uid = Users::user_in_work["org_uid"]:Users::user_in_work["uid"]:Users::max_uid;

    if (UsersCache::user_type == "nis")
    {
        // error popup
        Report::Message (sformat (_("Cannot delete the user %1. It must be done on the NIS server."), username));
        return `not_next;
    }

    // if the user has log on system
    string proc = lookup (SCR::Execute (.target.bash_output, sformat ("ps --no-headers -u %1", uid )), "stdout", "");
    if (size (proc) != 0)
    {
        // error popup
        Report::Error(_("You can not delete this user, because the user is present.
Please log off the user first."));
        delete = false;
        return `not_next;
    }

    boolean no_home = false;
    if ((UsersCache::user_type == "ldap" && !Users::ldap_file_server) ||
        // check if dir exists with this owner
        (lookup (SCR::Read(.target.stat, home), "uid", -1) != uid) )
        no_home = true;

    // if the user want to delete a system user
    if ( UsersCache::user_type == "system" )
    {
        // yes-no popup headline
        if(! Popup::YesNoHeadline(_("The marked user is a system user."),
        // yes-no popup contents
         _("Do you really want to delete this user?")))
        {
	    delete = false;
        }
    }
    else
    {
        if( home != "")
        {
        term contents = `HBox(
            `HSpacing(3),
            `VBox(`VSpacing(1),
                // question popup. %^1 is username
                `Left(`Heading(sformat(_("Delete the user %1?"), username ))),
                `VSpacing(0.5),
                no_home ? `VSpacing (0) :
                // checkbox label
                `Left (`CheckBox(`id(`delete_home), sformat(_("delete &home directory
%1"), home ) )),
                `VSpacing(1),
                `HBox(
                    `Bottom(`PushButton(`id(`ok),`opt(`key_F10),
                        Label::YesButton()  )),
                    `Bottom(`PushButton(`id(`cancel), `opt(`key_F9),
                        Label::NoButton()))
                )
            ),
            `HSpacing(3));

        UI::OpenDialog(`opt( `decorated ), contents );
        any ret  = UI::UserInput();
        delete_home = no_home ? false : UI::QueryWidget(`id(`delete_home), `Value);

        UI::CloseDialog();

        if( ret == `cancel )
        {
            delete = false;
        }
        }
        else
        {
            // yes-no popup. %1 is username
            if( ! Popup::YesNo(sformat(_("
Do you really want to delete the user %1?
"), username )))
        {
            delete = false;
        }
        }
    }

    if (delete)
    {
        Users::user_in_work["what"] = "delete_user";
        Users::user_in_work["delete_home"] = delete_home;
        return `delete;
    }
    return `not_next;
}


/**
 * Popup for deleting group
 * @return symbol for sequencer
 */
global define symbol DeleteGroupPopup() ``{

    boolean delete = true;
    integer gid = Users::group_in_work["gid"]:-1;

    // if no user is in this group
    if (Users::group_in_work["userlist"]:"" == "" &&
        Users::group_in_work["more_users"]:"" == "")
    {
        //if the group is a system group ask the user ..
        if ( UsersCache::group_type == "system" )
        {
            // yes-no popup headline
            if(!Popup::YesNoHeadline(_("The marked group is a system group."),
            /// yes-no popup contents
            _("Really delete this group?")))

                return `not_next;
        }
        else
        {
            // yes-no popup, %1 si group name
            if (! Popup::YesNo(sformat(_("
Do you really want to delete the group %1?
"), Users::group_in_work["groupname"]:"")))

                return `not_next;
        }
    }
    else
    {
        // warning popup
        Popup::Warning(_("You cannot delete this group because
there are users in the group.
Remove these users from the group first.
"));
        return `not_next;
    }

    Users::group_in_work["what"] = "delete_group";
    return `delete;
}

/**
 * Popup for configuration user/group filter for making the LDAP search
 * @return modified?
 */
global define boolean LDAPSearchFilterPopup () ``{

    string default_user_f = Users::ldap_default_user_filter;
    string default_group_f = Users::ldap_default_group_filter;

    string user_f = Users::ldap_user_filter;
    string group_f = Users::ldap_group_filter;

    if (user_f == "")
	user_f = default_user_f;
    if (group_f == "")
	group_f = default_group_f;

    boolean ret = false;

    string help_text = LDAPSearchFilterPopupHelp();

    UI::OpenDialog(`opt(`decorated), `HBox(`HSpacing(1.5),
        `VBox(
            `HSpacing(50),
            `VSpacing(0.5),
	        `Frame(_("Filter for Searching the LDAP Directory"), `HBox(
		  `HSpacing (0.5),
		  `VBox(
		    `VSpacing(0.5),
		    // textentry label
		    `TextEntry (`id(`userf), _("&User Filter"), user_f),
		    `VSpacing(0.5),
		    // textentry label
		    `TextEntry (`id(`groupf), _("&Group Filter"), group_f),
		    `VSpacing(0.5)
		  ),
		  `HSpacing(0.5)
		)),
            `VSpacing(0.5),
	    `HBox(
		`PushButton(`id(`ok), `opt(`default,`key_F10), Label::OKButton()),
	        `PushButton(`id(`cancel), `opt(`key_F9), Label::CancelButton()),
	        `PushButton(`id(`help), `opt(`key_F2), Label::HelpButton()),
	        `PushButton(`id(`read), `opt(`key_F3), _("From &Server"))
	    ),
            `VSpacing(0.5)),
        `HSpacing(1.5)
    ));

    symbol button = nil;
    do
    {
	button = UI::UserInput();

	if ( button == `help )
        {
	    Wizard::ShowHelp( help_text );
	}
	if ( button == `read )
	{
	    if (Users::ReadLDAPFilters())
	    {
		UI::ChangeWidget(`id (`userf), `Value,
		    Users::ldap_default_user_filter);
		UI::ChangeWidget(`id (`groupf),`Value,
		    Users::ldap_default_group_filter);
	    }
	}
	if ( button == `ok)
	{
	    string new_user_f = UI::QueryWidget(`id (`userf), `Value);
	    string new_group_f = UI::QueryWidget(`id (`groupf), `Value);
	    if (new_user_f != user_f)
	    {
		if (!issubstring (new_user_f, default_user_f))
		{
		    // error popup
		    Popup::Error (_("New user filter doesn't contain the default user filter.
Please try again."));
		    UI::SetFocus (`id(`userf));
		    continue;
		}
		user_f = new_user_f;
	    }
	    if (new_group_f != group_f)
	    {
		if (!issubstring (new_group_f, default_group_f))
		{
		    // error popup
		    Popup::Error (_("New group filter doesn't contain the default group filter.
Please try again."));
		    UI::SetFocus (`id(`groupf));
		    continue;
		}
		group_f = new_group_f;
	    }
	    // checks are OK, let's update the values now
	    if (user_f != Users::ldap_user_filter)
	    {
		Users::ldap_user_filter = user_f;
		ret = true;
	    }
	    if (group_f != Users::ldap_group_filter)
	    {
		Users::ldap_group_filter = group_f;
		ret = true;
	    }
	}
    } while ( button != `ok && button != `cancel );

    UI::CloseDialog();
    return ret;
}


/**
 * Dialog for definition of customized view
 * @param what "user" or "group"
 * @return true if customs were modified
 */
global define boolean CustomizePopup(string what) ``{

    term view = `VBox();
    string label = "";
    list sets = [];
    list custom_sets = [];
    map set_to_string = $[];

    if (what == "user")
    {
        sets = filter (`set, Users::available_usersets, ``(set != "custom"));
        custom_sets = Users::user_custom_sets;
        set_to_string = Users::userset_to_string;
		// Frame label
        label = _("Customize the View of the User List");
    }
    else
    {
        sets = filter (`set, Users::available_groupsets, ``(set != "custom"));
        custom_sets = Users::group_custom_sets;
        set_to_string = Users::groupset_to_string;
		// Frame label
        label = _("Customize the View of the Group List");
    }

    foreach (`set, sets, ``{
        view = add (view, `VSpacing(0.5));
        if ( contains (custom_sets, set) )
	    view = add (view, `Left(`CheckBox(`id(set), set_to_string [set]:"",
		true)));
        else
	    view = add (view, `Left(`CheckBox(`id(set), set_to_string [set]:"",
		false)));
    });
    view = add (view, `VSpacing(0.5));

    UI::OpenDialog(`opt(`decorated), `HBox(`HSpacing(1.5),
        `VBox(
            `HSpacing(40),
            `VSpacing(0.5),
	        `Frame(label, view),
            `VSpacing(0.5),
	    `HBox(
	        `PushButton(`id(`ok), `opt(`default,`key_F10), Label::OKButton()),
		`PushButton(`id(`cancel), `opt(`key_F9), Label::CancelButton())
	    ),
            `VSpacing(0.5)),
        `HSpacing(1.5)
    ));
    any ret = UI::UserInput();
    boolean modified = false;
    list new_customs = [];
    if (ret == `ok)
    {
        foreach (`set, sets, ``{
            if (UI::QueryWidget(`id(set), `Value))
            {
                new_customs = add (new_customs, set);
                if (! contains (custom_sets, set)) modified = true;
            }
            else
            {
                if (contains (custom_sets, set)) modified = true;
            }
        });;
    }
    UI::CloseDialog();
    if (modified)
    {
        modified = Users::ChangeCustoms (what, new_customs);
    }
    return modified;
}

/**
 * Define the switch between Users and Groups administration.
 * This is used in UsersDialog and GroupsDialog.
 * @param what what can be `users or `groups
 * @return term RadioButtonGroup widget
 */
global define term rbUsersGroup(symbol what) ``{
    string curr = "";
    if (what == `users)
    {
        if (size (Users::current_users) == 1)
            curr = Users::userset_to_string[Users::current_users[0]:"custom"]:"";
        else
            curr = Users::userset_to_string["custom"]:"";
    }
    else
    {
        if (size (Users::current_groups) == 1)
            curr = Users::groupset_to_string[Users::current_groups[0]:"custom"]:"";
        else
            curr = Users::groupset_to_string["custom"]:"";
    }
    curr = deletechars (curr, "&");
    return
        `HBox(
        `Left(`RadioButtonGroup(`HBox(
            `RadioButton(`id(`switch_users), `opt(`notify),
                // pushbutton
                _("&Users Administration"), what == `users),
            `RadioButton(`id(`switch_groups), `opt(`notify),
                // pushbutton
                _("&Groups Administration"), what == `groups)
//	))));
        ))),
        `Right(`Label(`id(`curr_f), sformat(_("Filter: %1"), curr)))
        );
}

/**
 * Save focus of main dialog's table
 * @param table_input integer
 * @param ret any
 * @param what symbol
 */
global define void save_focus(integer table_input, any ret, symbol what) ``{

    integer l_focusline = 0;

    if ( what == `group )
        l_focusline = UsersCache::focusline_group;
    if ( what == `user )
        l_focusline = UsersCache::focusline_user;

    if ( is ( table_input, integer ))
    {
        l_focusline = table_input;
        if( ret == `delete )
            l_focusline = nil;
    }
    if( l_focusline < 0 )
    {
        l_focusline = nil;
    }

    if ( what == `group )
        UsersCache::focusline_group = l_focusline;
    if ( what == `user )
        UsersCache::focusline_user = l_focusline;
}

/* EOF */
}
