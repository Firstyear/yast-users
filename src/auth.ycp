/**
 *
 * $Id$
 *
 * Module:
 *
 * Author:
 *
 * Purpose:
 */

{

textdomain "users";

import "Wizard";
import "Users";
import "Mode";
include "ui/common_messages.ycp";
//include "users/nis_routines.ycp";


    // Help text 1/5
    string help_text = _("<p>
<b>User lists</b><br>
Here you can see the sources of user's accounts. The possible values here can be:
</p>");
    // help text 2/5  - do not translate Compat
    help_text = help_text + _("<p>
<b>Local files</b> - data are stored in the local files <i>/etc/passwd</i> and <i>/etc/shadow</i><br>
<b>NIS</b> - user data can be retrieved from NIS server<br>
<b>Compat</b> - both previous sources can be used<br>
<b>LDAP</b> - user database is on the LDAP server
</p>");
    // help text 3/5
    help_text = help_text + _("<p>
For more information, look at the file <i>/etc/nsswitch.conf</i> and its documentation.
</p>");

    // help text 4/5
    help_text = help_text + _("<p>
<b>Additional authentication</b>
This is the partial list of your PAM settings. It shows the authentication types, which can be used by users on your system. As a default, users are authenticated by comparing their passwords with the ones saved in local files or on the NIS server.<br>
Other possibility for authenication is using the LDAP server or Kerberos.
</p>");

    // help text 5/5
    help_text = help_text + _("<p>
<b>Changing the values</b><br>
You can configure these settings by running appropriate modules. Select the module with the <b>Configure</b> button.
</p>");

    //    list supported_users_sources = [ "nis", "ldap", "files", "compat" ];

    define void reload_config() ``{

        // checking /etc/nsswitch.conf
        list users_sources = Users::GetUsersSources ();
        map to_string = $[
            // just the name (do not translate)
            "nis"   : _("NIS"),
            // just the name (do not translate)
            "ldap"  : _("LDAP"),
            // the source of users sets
            "files" : _("Local files"),
            // the source of users sets (do not translate)
            "compat": _("Compat"),
            // just the name (do not translate)
            "krb5"  : _("Kerberos")
         ];

        string sources_text = "";
        foreach (`source, users_sources, ``{

    //        UI::ChangeWidget(`id(passwd_source), `Value, true);
            /*
            if (passwd_source == "compat") // what doues this _really_ mean?
            {
                UI::ChangeWidget(`id("nis"), `Value, true);
                UI::ChangeWidget(`id("files"), `Value, true);
            }
            */
            sources_text = sources_text + to_string [source]:"" + "<br>";
        });
        UI::ChangeWidget(`id(`sources), `Value, sources_text);

        // check for pam settings (/etc/security/pam_unix2.conf)
        map pam_auth_map = $[];
        list pam_auth_list = SCR::Read (.pam.all.auth.pam_unix);
        if (pam_auth_list != [] && pam_auth_list != nil)
            pam_auth_map = select (pam_auth_list,0,$[]);

        list pam_args = splitstring ( pam_auth_map ["arguments"]:"", " ");
        string auth_text = "";

        if ( contains (pam_args, "use_ldap"))
            auth_text = auth_text + to_string ["ldap"]:"" + "<br>";

        // and this looks to /etc/pam.d/login ...
        pam_auth_list = SCR::Read (.pam.login);
        boolean auth_krb = false;
        foreach (`line, pam_auth_list, ``{
            if (issubstring (line["module"]:"", "pam_krb5.so") &&
                line["type"]:"" == "auth")
                auth_krb = true;
        });
        if (auth_krb)
            auth_text = auth_text + to_string ["krb5"]:"" + "<br>";

        UI::ChangeWidget(`id(`auth), `Value, auth_text);
    }

    term contents = `HBox(
        `HSpacing(1),
        `VBox(
        `VSpacing( 1 ),
        `HBox( `HSpacing(12),
        // frame label
        `Frame( _("Users lists"),
            `HBox(
            `HSpacing(1),
            `VBox(
                `VSpacing( 0.5 ),
                /*
                `CheckBox (`id("nis"), "NIS", false),
                `CheckBox (`id("ldap"), "LDAP", false),
                `CheckBox (`id("files"), _("local files"), false),
                `CheckBox (`id("compat"), _("compat"), false),*/
                `RichText (`id(`sources), ""),
                `VSpacing( 0.5 )
                ),
            `HSpacing(1))
        ),
        `HSpacing (12)),

        `VSpacing( 1 ),
        `HBox( `HSpacing(12),
        // frame label
        `Frame( _("Additional authentication (PAM settings)"),
            `HBox(
            `HSpacing (1),
            `VBox(
                `VSpacing( 0.5 ),
                `RichText (`id(`auth), ""),
                `VSpacing( 0.5 )
                ),
            `HSpacing (1))
        ),
        `HSpacing (12)),

        `VSpacing( 1 ),
        // button label
        `MenuButton(_("&Configure ..."), [
            `item(`id(`conf_nis), "NIS"),
            `item(`id(`conf_ldap), "LDAP"),
            `item(`id(`conf_krb), "Kerberos")]),
        `VSpacing( 1 )
        )
    );

    // dialog title
    string title = _("Expert Login Options");

    if (!Mode::cont)
    {
        Wizard::CreateDialog();
        Wizard::SetContentsButtons (title, contents, help_text,
            UI::BackButtonLabel(), UI::FinishButtonLabel());
        Wizard::ReplaceAbortButton(`Empty ());
    }
    else
// Wizard::SetContents(title, contents, helptext, Args(0), Args(1));
        Wizard::SetContents (title, contents, help_text, true, true);

    reload_config();

    symbol button = nil;

    do
    {
        button = UI::UserInput();

        if ( button == `conf_nis)
        {
            WFM::CallModule("nis", []);
            reload_config ();
        }

        if ( button == `conf_ldap)
        {
            WFM::CallModule("ldap", []);
            reload_config ();
        }

        if ( button == `conf_krb)
        {
            WFM::CallModule("kerberos", []);
            reload_config ();
        }

        if ( button == `conf_krb)
        {

        }

        if ( button == `abort )
        {
            if (Mode::cont)
            {
                if (CallFunction(`inst_confirm_abort(`incomplete)))
                    return `abort;
            }
            else
                return `abort;
        }

    } while ( button != `next && button != `abort && button != `back );

    if (!Mode::cont)
        UI::CloseDialog();

    return button;
}
